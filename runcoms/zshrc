#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#


[ -d /usr/local/share/zsh-completions ] && fpath=(/usr/local/share/zsh-completions $fpath)
[ -d /usr/local/share/zsh/site-functions ] && fpath=(/usr/local/share/zsh/site-functions $fpath)
[ -d /opt/homebrew/share/zsh/site-functions ] && fpath=(/usr/local/share/zsh/site-functions $fpath)
[ -d $HOME/.zsh/completion ] && fpath=($HOME/.zsh/completion $fpath)
# run 'rm -f ~/.zcompdump; compinit' after updating completion file

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

autoload -Uz promptinit
promptinit

zstyle ':completion:*' verbose yes
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*' group-name ''

autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end
bindkey "^]" autosuggest-accept

# fzf
function fzf-history-selection() {
    BUFFER=`history -n 1 | tac  | awk '!a[$0]++' | fzf --height=40% --reverse --prompt="History > "`
    CURSOR=$#BUFFER
    zle reset-prompt
}
zle -N fzf-history-selection
bindkey '^R' fzf-history-selection

# cdr
if [[ -n $(echo ${^fpath}/chpwd_recent_dirs(N)) && -n $(echo ${^fpath}/cdr(N)) ]]; then
    autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
    add-zsh-hook chpwd chpwd_recent_dirs
    zstyle ':completion:*' recent-dirs-insert both
    zstyle ':chpwd:*' recent-dirs-default true
    zstyle ':chpwd:*' recent-dirs-max 1000
    zstyle ':chpwd:*' recent-dirs-file "$HOME/.cache/chpwd-recent-dirs"
fi

function fzf-cdr () {
    local selected_dir="$(cdr -l | sed 's/^[0-9]\+ \+//' | fzf --height=40% --reverse --prompt="cdr >" --query "$LBUFFER")"
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
}
zle -N fzf-cdr
bindkey '^E' fzf-cdr

REPORTTIME=1

# ROS 1 config
if [ -d ~/catkin_ws -o -d ~/ros ]; then
    if command -v ip >/dev/null 2>&1; then
        export MYWLAN0IP=`ip a show "$(ip a | grep -o -E "wl.*:" | sed -e "s/://g" | head -n1)" 2>/dev/null | grep -o -E "([0-9]+\.){3}[0-9]+" | head -n1`
        export MYETH0IP=`ip a show "$(ip a | grep -o -E "en.*:" | sed -e "s/://g" | head -n1)" 2>/dev/null | grep -o -E "([0-9]+\.){3}[0-9]+" | head -n1`
    fi

    # export ROS_IP=$(echo $MYETH0IP $MYWLAN0IP 127.0.0.1 | cut -d' ' -f1)
    # export ROS_MASTER_URI=http://$ROS_IP:11311
    # source /opt/ros/melodic/setup.zsh
    # source `catkin locate --shell-verbs`
fi

# ROS 2 config

#export ROS_DOMAIN_ID=0

function set_dds_discovery_range() {
    export ROS_AUTOMATIC_DISCOVERY_RANGE=$1
}

_set_dds_discovery_range() {
    local -a comps
    comps=(SUBNET LOCALHOST OFF)

    _values 'subcmd' $comps
}

compdef _set_dds_discovery_range set_dds_discovery_range


if [ -d /opt/ros/jazzy ]; then
    # https://github.com/ros2/ros2cli/issues/534#issuecomment-957516107
    source /opt/ros/jazzy/setup.zsh
    eval "$(register-python-argcomplete ros2)"
    eval "$(register-python-argcomplete colcon)"
    export export RCUTILS_COLORIZED_OUTPUT=1
fi

if [ -d /usr/share/colcon_cd ]; then
    source /usr/share/colcon_cd/function/colcon_cd-argcomplete.zsh
fi

if [ -d /usr/share/colcon_argcomplete/hook ]; then
    source /usr/share/colcon_argcomplete/hook/colcon-argcomplete.zsh
fi

if [[ "$OSTYPE" == "darwin"* ]];then
    [ -d /opt/homebrew/opt/curl/bin ] && export PATH="/opt/homebrew/opt/curl/bin:$PATH"
    [ -d /opt/homebrew/opt/gawk ] && export PATH="/opt/homebrew/opt/gawk/libexec/gnubin:$PATH"
    [ -d /opt/homebrew/bin/ ] && eval $(/opt/homebrew/bin/brew shellenv)
    [ -d /usr/local/opt/qt5 ] && export PATH=/usr/local/opt/qt5:$PATH
    [ -d /usr/local/sbin ] && export PATH=/usr/local/sbin:$PATH
    function display_notification () {
        osascript -e 'on run argv
        display notification current date as text with title item 1 of argv
        end run' -- "$*"
    }
    function with_display_notification () {
        "$@"
        display_notification "($?)" "$@"
    }
    export PROMPT='($(uname -m))'$PROMPT
fi

if [ -d ~/.poetry/bin ]; then
    export PATH=$HOME/.poetry/bin:$PATH
fi

if [ -d ~/.pyenv ]; then
    export PYENV_ROOT=~/.pyenv
    export PATH=$PYENV_ROOT/bin:$PATH
    eval "$(pyenv init -)"
    if [ -d ~/.pyenv/plugins/pyenv-virtualenv ]; then
        eval "$(pyenv virtualenv-init -)"
    fi
fi

if [ -d ~/.anyenv ]; then
    export PATH=$HOME/.anyenv/bin:$PATH
    eval "$(anyenv init -)"
fi

if [ -d ~/.pixi ]; then
    export PATH="~/.pixi/bin:$PATH"
fi

if command -v uv >/dev/null 2>&1; then
    eval "$(uv generate-shell-completion zsh)"
fi

# Run 'mise completion zsh > ~/.zsh/completion/_mise' for completion
if [ -x ~/.local/bin/mise ] || [ -x /opt/homebrew/bin/mise ]; then
    eval "$(mise activate zsh)"
fi

if [ -x ~/usr/local/bin ]; then
    export PATH=$HOME/usr/local/bin:$PATH
fi

if [ -x ~/.local/bin ]; then
    export PATH=$HOME/.local/bin:$PATH
fi

if [ -x ~/.go ]; then
    export GOPATH=$HOME/.go
    export PATH=$GOPATH/bin:$PATH
fi

if [ -x ~/go ]; then
    export GOPATH=$HOME/go
    export PATH=$GOPATH/bin:$PATH
fi

if [ -d ~/.codeium/windsurf/bin ]; then
    export PATH="$HOME/.codeium/windsurf/bin:$PATH"
fi

# set terminal.integrated.env.linux in Cursor's settings.json
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
    if [[ "$CURSOR_IDE" == "1" ]]; then
        if command -v cursor >/dev/null 2>&1; then
            src_path="$(cursor --locate-shell-integration-path zsh 2>/dev/null)"
            [[ -f "$src_path" ]] && source "$src_path"
        fi
    else
        if command -v code >/dev/null 2>&1; then
            src_path="$(code --locate-shell-integration-path zsh 2>/dev/null)"
            [[ -f "$src_path" ]] && source "$src_path"
        fi
    fi
fi

if command -v eza >/dev/null 2>&1; then
  eza() {
    if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
        if [[ -n "$(git status --porcelain 2>/dev/null)" ]]; then
            command eza --time-style long-iso --long --git --all "$@"
        return
      fi
    fi
    command eza --time-style long-iso "$@"
  }
fi

function mise-select() { [[ -z "$1" ]] && return 1 || mise use "$1@$(mise ls-remote "$1" | sort -rV | fzf)" }

function ghq-fzf() {
    local roots_re="$(
        ghq root --all \
            | sed 's/[.[\*^$()+?{}|]/\\&/g' \
            | paste -sd'|' -
    )"
    #local src="$(ghq root)/$(ghq list | fzf --preview "ls -laTp $(ghq root)/{} | tail -n+4 | awk '{print \$9\"/\"\$6\"/\"\$7 \" \" \$10}'")"
    #local src="$(ghq root)/$(ghq list | fzf --preview "eza --all --long --git --icons --time-style long-iso $(ghq root)/{} | awk '{print \$4\" \"\$5\"/\"\$6\" \"\$7 \" \" \$8}'")"
    local src=$(
        ghq list -p |
        awk -v re="^(${roots_re})/" '{d=$0; sub(re, "", d); print d "\t" $0}' |
        fzf --delimiter=$'\t' --with-nth=1 \
            --preview 'eza --all --long --git --icons --time-style long-iso -- {2}' |
        cut -f2
    ) || return
    if [ -n "$src" ]; then
      BUFFER=" cd $src"  # HIST_IGNORE_SPACE
      zle accept-line
    fi
    zle -R -c
}
zle -N ghq-fzf
bindkey '^g' ghq-fzf

if command -v gwq >/dev/null 2>&1; then
    source <(gwq completion zsh)
fi

if [ -f ~/.zshrc_local ]; then
    source $HOME/.zshrc_local
fi

# Do not save command history from Copilot Chat
if [[ -n "${COPILOT_RUN:-}" ]]; then
    unset HISTFILE
    HISTSIZE=0
    SAVEHIST=0
    # prompt/theme の更新を止める（prezto/steeef 由来を無効化）
    precmd_functions=(${precmd_functions:#prompt_steeef_precmd})
    precmd_functions=(${precmd_functions:#update_lprompt})
    PROMPT='%n@%m:%~%# '
    RPROMPT=''
fi
